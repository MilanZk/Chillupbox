// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.31'

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

//Plugin added to check if the project has the last dependencies. You can run a gradle task via terminal with:
// ./gradlew dependencyUpdates
plugins {
    id 'com.github.ben-manes.versions' version '0.21.0'
}
//This configuration is added for the ben-manes' plugin to ignore alpha, beta, rc, and so on updates
dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

apply from: 'dependencies.gradle'
apply plugin: 'com.github.ben-manes.versions'

def versionMajor = 0
def versionMinor = 2
def versionPatch = 1
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

allprojects {
    repositories {
        google()
        jcenter()
    }

    ext {
        androidApplicationId = 'com.company.mobile.android.appname'
        androidVersionCode = versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        androidVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
